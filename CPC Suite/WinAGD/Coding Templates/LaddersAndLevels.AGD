; AGD template for ladders and levels controls.
; By J. Cauldwell, December 2012.

; Player going up a ladder.
IF KEY UP
    IF LADDERABOVE
        IF Y <= TOPEDGE
            SCREENUP
            LET Y = BOTTOMEDGE
            EXIT
        ELSE
            LET IMAGE = 0      ; change this to your "going up" image.
            ANIMATE
            SPRITEUP
        ENDIF
    ENDIF
ENDIF

; Player going down a ladder.
IF KEY DOWN
    IF LADDERBELOW
        LET IMAGE = 0          ; change this to your "going down" image.
        ANIMATE
        SPRITEDOWN
    ENDIF
    IF Y > BOTTOMEDGE
        SCREENDOWN
        LET Y = TOPEDGE
        EXIT
    ENDIF
ENDIF

; Player moving right.
IF KEY RIGHT
    IF CANGORIGHT
        LET IMAGE = 0          ; change this to your "facing right" image.
        ANIMATE
        SPRITERIGHT
    ENDIF
    IF X > RIGHTEDGE
        SCREENRIGHT
        LET X = LEFTEDGE
        EXIT
    ENDIF
ENDIF

; Player moving left.
IF KEY 1
    IF X <= LEFTEDGE
        SCREENLEFT
        LET X = RIGHTEDGE
        EXIT
    ELSE
        IF CANGOLEFT
            LET IMAGE = 0      ; change this to your "facing left" image.
            ANIMATE
            SPRITELEFT
        ENDIF
    ENDIF
ENDIF

; Falling.
IF LADDERBELOW
ELSE
    FALL
ENDIF

IF Y >= BOTTOMEDGE
    SCREENDOWN
    LET Y = TOPEDGE
    EXIT
ENDIF
IF Y <= TOPEDGE
    SCREENUP
    LET Y = BOTTOMEDGE
    EXIT
ENDIF

; Deadly blocks.
IF DEADLY
    KILL
ENDIF
